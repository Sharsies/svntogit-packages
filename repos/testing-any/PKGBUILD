# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.5
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('ba333563827058bbf67a521efcd4690ffba874ca61d8c8d4fb48881cd08fc3d7b7f5a1722b1bd0b35c04f43ba0184e2aa77e0ce6069d79578a075339352c4d37')

prepare() {
  cd astroid-astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-astroid-$pkgver
  pytest
}

package() {
  cd astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
