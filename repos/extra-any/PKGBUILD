# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=sof-firmware
pkgver=1.7
pkgrel=2
pkgdesc="Sound Open Firmware"
arch=(any)
url="https://www.sofproject.org/"
license=(custom:BSD custom:ISC)
options=(!strip)
_tarname=sof-bin-$pkgver
source=($_tarname.tar.gz::https://github.com/thesofproject/sof-bin/archive/v$pkgver.tar.gz)
sha256sums=('895d0199c06fc9b8bd8452fffff4f0d53134af451f63ffd1b78f04b10ef6c251')

package() {
  cd $_tarname

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cp -a sof-v$pkgver "$fwdir/sof"
  cp -a sof-tplg-v$pkgver "$fwdir/sof-tplg"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
